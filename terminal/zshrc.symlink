DOTFILES_DIR=$HOME/.dotfiles

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES_DIR/**/*.zsh)

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

ZSH="$HOME/.oh-my-zsh"

# configure plugins
plugins=("${(@f)$(
find $DOTFILES_DIR -maxdepth 1 -not -name '.git' -type d -exec basename {} \; | awk -v zsh=${ZSH} '{print zsh"/plugins/"$1}' | xargs ls -d 2>/dev/null | xargs -n 1 basename | sort | uniq

find $DOTFILES_DIR -maxdepth 2 -name oh-my-zsh.plugins -exec cat {} \; -exec echo \;
)}")

# configure stuff before we launch oh-my-zsh
for file in ${(M)config_files:#*/oh-my-zsh.zsh}
do
  source $file
done

# load oh-my-zsh
OH_MY_ZSH="$ZSH/oh-my-zsh.sh"
source "$OH_MY_ZSH"

## load dotfiles ##

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# put the bin/ directories first on the path
#for d in $(dotfiles)
#do
#  export PATH=$d/bin:$PATH
#done

# load everything else
for file in ${${${config_files:#*/path.zsh}:#*/completion.zsh}:#*/oh-my-zsh.zsh}
do
  source $file
done

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files